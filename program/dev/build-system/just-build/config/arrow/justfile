# set shell := ["/usr/bin/env fish", "-c"]
GREEN :='\033[1;32m'
NORMAL := '\033[0m'

build: build-cpp build-python
	#!/usr/bin/env fish
	printf "{{GREEN}}All done.{{NORMAL}}\n"

build-cpp: clean clone
	#!/usr/bin/env fish
	printf "{{GREEN}}Building cpp...{{NORMAL}}\n"
	conda activate pyarrow-dev
	set -x ARROW_HOME $CONDA_PREFIX
	mkdir ~/git/arrow/cpp/build
	pushd ~/git/arrow/cpp/build
	cmake -DCMAKE_INSTALL_PREFIX=$ARROW_HOME \
		-DCMAKE_INSTALL_LIBDIR=lib \
		-DARROW_BUILD_TESTS=ON \
		-DARROW_PARQUET=ON \
		-DARROW_PYTHON=ON \
		-DARROW_WITH_SNAPPY=ON \
		..
	make -j4
	make install
	popd
	conda deactivate

build-python: clean clone
	#!/usr/bin/env fish
	printf "{{GREEN}}Building python...{{NORMAL}}\n"
	conda activate pyarrow-dev
	set -x ARROW_HOME $CONDA_PREFIX
	set -x PYARROW_WITH_BUILD_TESTS 1
	set -x PYARROW_WITH_PARQUET 1
	set -x PYARROW_WITH_PYTHON 1
	pushd ~/git/arrow/python
	python setup.py build_ext --inplace
	popd
	conda deactivate

clean:
	#!/usr/bin/env fish
	rm -rf ~/git/arrow/cpp/build
	rm -rf ~/git/arrow/python/build

clone:
	#!/usr/bin/env fish
	if ! test -d ~/git/arrow
		git clone https://github.com/apache/arrow ~/git/arrow
	end

test-all:
	#!/usr/bin/env fish
	conda activate pyarrow-dev
	pytest ~/git/arrow/python/pyarrow
	conda deactivate

test TEST:
	#!/usr/bin/env fish
	conda activate pyarrow-dev
	pytest ~/git/arrow/python/pyarrow/tests/{{TEST}}
	conda deactivate
